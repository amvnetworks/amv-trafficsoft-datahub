import io.franzbecker.gradle.lombok.task.DelombokTask

buildscript {
    ext {
        trafficsoftRestVersion = '0.6.13'
        vertxVersion = '3.5.0'
        amvVertxVersion = '0.0.1'
        jacksonVersion = '2.8.10'
        springBootVersion = '1.5.8.RELEASE'
        reactorVersion = '3.1.0.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-credentials-plugin:1.0.3'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id 'nebula.lint' version '6.8.0'
    id 'nebula.release' version '6.0.0'
    id 'nebula.dependency-lock' version '4.9.3'
    id 'nebula.info' version '3.6.0'
    id 'nebula.project' version '3.4.0'
    id 'nebula.optional-base' version '3.2.0'
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'findbugs'
}

description = 'Parent package for handling AMV Trafficsoft xfcd data deliveries'

group = 'org.amv.trafficsoft.datahub'

contacts {
    'devlopment@amv-networks.com' {
        moniker 'AMV Networks GmbH'
        role 'owner'
        github 'amvnetworks'
    }
}

allprojects {
    apply plugin: 'nu.studer.credentials'

    group 'org.amv.trafficsoft.datahub'

    configurations.all {
        resolutionStrategy {
            preferProjectModules()
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            force 'com.netflix.archaius:archaius-core:0.6.6',
                    'commons-codec:commons-codec:1.10',
                    'org.slf4j:slf4j-api:1.7.25',
                    'com.google.guava:guava:22.0',
                    'io.dropwizard.metrics:metrics-core:3.2.5',
                    'org.eclipse.aether:aether-api:1.1.0',
                    'org.eclipse.aether:aether-spi:1.1.0',
                    'org.eclipse.aether:aether-util:1.1.0',
                    'org.eclipse.aether:aether-impl:1.1.0',
                    'com.fasterxml.jackson.core:jackson-core:2.8.10',
                    'com.fasterxml.jackson.core:jackson-databind:2.8.10',
                    'com.fasterxml.jackson.core:jackson-annotations:2.8.10',
                    'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.10',
                    'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.10',
                    'com.fasterxml.jackson.module:jackson-module-parameter-names:2.8.10',
                    'io.reactivex:rxjava:1.3.0',
                    'org.reactivestreams:reactive-streams:1.0.1',
                    'org.apache.commons:commons-lang3:3.5',
                    'org.hamcrest:hamcrest-core:1.3',
                    'commons-logging:commons-logging:1.2',
                    'org.apache.commons:commons-compress:1.14',
                    'com.google.code.findbugs:jsr305:3.0.2',
                    'org.jetbrains.kotlin:kotlin-stdlib:1.3.70',
                    'com.squareup.okio:okio:1.17.3'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'nebula.dependency-lock'
    apply plugin: 'nebula.lint'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // ---- prepare task graph - disable long running tasks on demand
    gradle.taskGraph.whenReady { graph ->
        def isMinimalBuild = project.hasProperty("minimal")

        def enableAdditionalLongRunningTasks = !isMinimalBuild

        if (!enableAdditionalLongRunningTasks) {
            findbugsMain.enabled = false
            //checkstyleMain.enabled = false
            javadoc.enabled = false
        }
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile 'com.google.guava:guava:22.0'
        compile 'io.dropwizard.metrics:metrics-core:3.2.5'
        compileOnly 'org.projectlombok:lombok:1.16.16'

        compile "io.projectreactor:reactor-core:${reactorVersion}"
        compile "io.projectreactor.addons:reactor-adapter:${reactorVersion}"
        compile "io.projectreactor.addons:reactor-extra:${reactorVersion}"

        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'pl.pragmatists:JUnitParams:1.0.6'

        // TODO: commons-lang should be a testCompile dependency:
        // only used for RandomUtils in tests
        compile 'org.apache.commons:commons-lang3:3.5'

        testCompile "io.vertx:vertx-unit:${vertxVersion}"
        testCompile "io.projectreactor:reactor-test:${reactorVersion}"

        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    }

    lombok {
        version = '1.16.16'
        sha256 = 'fbf682c7ff113e5187a9c4b4cbd7a8ad782abd8ccff91b9fa7289a4a6aa8c19a'
    }

    task delombok(type: DelombokTask, dependsOn: compileJava) {
        ext.outputDir = file("$buildDir/delombok")
        outputs.dir(outputDir)
        sourceSets.main.java.srcDirs.each {
            inputs.dir(it)
            args(it, '-d', outputDir)
        }
    }

    task delombokHelp(type: DelombokTask) {
        args '--help'
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        exclude 'lombok.config'
        failOnError = false
    }

    jar.doFirst {
        // other manifest attributes will be populated by nebula.info plugin
        // see https://github.com/nebula-plugins/gradle-info-plugin
        def vendor = 'AMV Networks GmbH'
        manifest {
            attributes(
                    'Specification-Title': project.description,
                    'Specification-Version': project.version,
                    'Specification-Vendor': vendor,
                    'Implementation-Vendor': vendor
            )
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    task testJar(type: Jar) {
        classifier = 'test'
        from sourceSets.test.output
    }

    artifacts {
        archives /*javadocJar, */ sourcesJar, testJar
    }

    findbugs {
        effort = 'min'
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    task subprojectsDependencies(type: DependencyReportTask) {

    }

    test {
        // Generate code coverage reports ... run with jacoco
        jacocoTestReport {
            additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
            reports {
                xml.enabled true
                csv.enabled false
                html.destination file("${buildDir}/reports/jacoco/html")
            }
            executionData = files("${buildDir}/jacoco/test.exec")
        }

        // * enable on demand
        // testLogging.showStandardStreams = true
        finalizedBy jacocoTestReport
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "${nexusUrl}/repository/releases/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(url: "${nexusUrl}/repository/snapshots/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.version = project.version
                pom.artifactId = project.name
                pom.groupId = project.group
            }
        }
    }
}
